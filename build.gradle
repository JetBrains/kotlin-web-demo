/*
 * Copyright 2000-2016 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.jk1:tcdeps:0.9.1"
    }
}

def kotlinVersions = file('kotlin.web.demo.backend/compilers/versions').list();

allprojects {
    apply plugin: 'java'
    repositories {
        mavenCentral()
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
        }
    }

    dependencies {
        compile 'commons-logging:commons-logging:1.2'
    }
}

for (kotlinVersion in kotlinVersions) {
    def projectsName = ":kotlin.web.demo.backend:compilers:versions:$kotlinVersion"
    project(projectsName) {
        apply plugin: 'com.github.jk1.tcdeps'
        repositories {
            teamcityServer {
                url = 'http://teamcity.jetbrains.com'
            }
        }
        configurations {
            kotlin
            compile.extendsFrom(kotlin)
        }
        dependencies {
            compile project(":kotlin.web.demo.backend:compilers")
            kotlin tc("bt345:$kotlinVersion:internal/kotlin-compiler-before-shrink.jar")
            kotlin tc("bt345:$kotlinVersion:kotlin-plugin-${kotlinVersion}.zip!/Kotlin/lib/kotlin-plugin.jar")
            kotlin tc("bt345:$kotlinVersion:kotlin-plugin-${kotlinVersion}.zip!/Kotlin/lib/kotlin-runtime.jar")
            kotlin tc("bt345:$kotlinVersion:kotlin-plugin-${kotlinVersion}.zip!/Kotlin/kotlinc/lib/kotlin-reflect.jar")
            kotlin tc("bt345:$kotlinVersion:kotlin-plugin-${kotlinVersion}.zip!/Kotlin/kotlinc/lib/kotlin-test.jar")
//            compile tc("bt345:$kotlinVersion:kotlin-plugin-$kotlinVersion.zip!")
        }
    }
}

project("kotlin.web.demo.backend") {
    apply plugin: 'war'
    webAppDirName = 'web'

    dependencies {
        compile project(":kotlin.web.demo.backend:compilers")
        compile project(":kotlin.web.demo.executors")
        compile project(":kotlin.web.demo.common")
        providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    }

    war {
        from(project(':kotlin.web.demo.executors').projectDir) {
            include 'executors.policy.template'
        }

        for(kotlinVersion in kotlinVersions) {
            def wrapperProject = project(":kotlin.web.demo.backend:compilers:versions:$kotlinVersion")
            from(wrapperProject.sourceSets.main.output) {
                into("WEB-INF/kotlin-wrappers/$kotlinVersion/classes")
            }
            from(wrapperProject.projectDir.getAbsolutePath() + '/js') {
                into "WEB-INF/kotlin-wrappers/$kotlinVersion/js"
            }
            from(wrapperProject.configurations['kotlin']) {
                into "WEB-INF/kotlin-wrappers/$kotlinVersion/kotlin"
            }
        }
    }
}

project("kotlin.web.demo.executors") {
    apply plugin: 'com.github.jk1.tcdeps'
    repositories {
        teamcityServer {
            url = 'http://teamcity.jetbrains.com'
        }
    }
    dependencies {
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.7.4'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.4'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.7.4'
        compile group: 'junit', 'name': 'junit', 'version': '4.12'
    }
}

project("kotlin.web.demo.common") {
    dependencies {
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.7.4'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.4'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.7.4'
        compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    }
}

project(':kotlin.web.demo.backend:compilers:versions').jar { onlyIf { false } }