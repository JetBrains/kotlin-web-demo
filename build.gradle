/*
 * Copyright 2000-2016 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    apply from: 'versions.gradle'
    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "gradle.plugin.com.github.jk1:tcdeps:0.15"
    }
}

ext.kotlinConfigs = gradle.ext.kotlinConfigs
ext.kotlinVersions = kotlinConfigs.collect { it.version }

allprojects {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
    }
}

configure(allprojects.findAll({
    it.name != 'kotlin.web.demo.frontend'
})) {
    apply plugin: 'java'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    sourceSets {
        test {
            java {
                srcDir 'test'
            }
        }
    }

    dependencies {
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.7.4', transitive: true
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.4', transitive: true
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.7.4', transitive: true
        compile 'commons-logging:commons-logging:1.2'
    }
}

configure([
        project(':kotlin.web.demo.server'),
        project(':kotlin.web.demo.backend'),
        project(':kotlin.web.demo.common'),
        project(':kotlin.web.demo.executors'),
        project(':kotlin.web.demo.backend:compilers'),
        project(':kotlin.web.demo.test')
]) {
    apply plugin: 'kotlin'
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.7.4"
    }
}

configure([
        project(':kotlin.web.demo.server'),
        project(':kotlin.web.demo.backend'),
        project(':kotlin.web.demo.backend:compilers'),
        project(':kotlin.web.demo.test')
]) {
    dependencies {
        compile project(':kotlin.web.demo.common')
    }
}

for (config in kotlinConfigs) {
    def build = config.build
    def version = config.version
    def buildType = config.buildType ?: "bt345"
    def pluginBuild = config.pluginBuild ?: config.build
    def projectsName = ":kotlin.web.demo.backend:compilers:versions:$version"
    project(projectsName) {
        apply plugin: 'com.github.jk1.tcdeps'

        repositories {
            teamcityServer {
                url = 'http://teamcity.jetbrains.com'
            }
        }
        configurations {
            kotlinJVM
            kotlinJS
        }

        dependencies {
            compile project(":kotlin.web.demo.backend:compilers")

            def root = "$buildType:$build:kotlin-plugin-${pluginBuild}.zip!/Kotlin/kotlinc/lib"

            if (config.useCompilerBeforeShrink) {
                kotlinJVM tc("$buildType:$build:internal/kotlin-compiler-before-shrink.jar")
            }
            else {
                kotlinJVM tc("$root/kotlin-compiler.jar")
            }
            kotlinJVM tc("$buildType:$build:kotlin-plugin-${pluginBuild}.zip!/Kotlin/lib/kotlin-plugin.jar")

            kotlinJVM tc("$root/kotlin-runtime.jar")
            kotlinJVM tc("$root/kotlin-reflect.jar")
            kotlinJVM tc("$root/kotlin-test.jar")
            kotlinJVM tc("$root/kotlin-jslib.jar")

            if (config.hasScriptJar) {
                kotlinJVM tc("$root/kotlin-script-runtime.jar")
            }

            kotlinJS tc("$root/kotlin-jslib.jar")

            if(!version.startsWith("1.0")) {
                kotlinJVM "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.17"
            }
            if(config.stdlibVersion) {
                kotlinJVM group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: config.stdlibVersion
            }

            compile fileTree(dir: projectDir.toString() + File.separator + "kotlin", include: '*.jar')
        }
    }
}

// Idea can't resolve tc dependencies, so we have to download them
// into folder and add all jars from this folder as dependencies.
task copyKotlinLibs() {
    for (kotlinVersion in kotlinVersions) {
        def wrapperProject = project(":kotlin.web.demo.backend:compilers:versions:$kotlinVersion")
        copy {
            from wrapperProject.configurations.kotlinJVM
            into wrapperProject.projectDir.toString() + File.separator + "kotlin"
        }
        copy {
            from(wrapperProject.configurations['kotlinJS'].collect { zipTree(it) })
            into wrapperProject.projectDir.toString() + File.separator + "js"
        }
    }
}

project("kotlin.web.demo.backend") {
    apply plugin: 'war'

    dependencies {
        compile project(":kotlin.web.demo.backend:compilers")
        compile project(":kotlin.web.demo.executors")
        providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    }

    war {
        archiveName "WebDemoBackend.war"

        from(project(':kotlin.web.demo.executors').projectDir) {
            include 'executors.policy.template'
        }

        for (kotlinVersion in kotlinVersions) {
            def wrapperProject = project(":kotlin.web.demo.backend:compilers:versions:$kotlinVersion")
            from(wrapperProject.sourceSets.main.output) {
                into("WEB-INF/kotlin-wrappers/$kotlinVersion/classes")
            }
            from(wrapperProject.configurations['kotlinJS'].collect { zipTree(it) }) {
                into "WEB-INF/kotlin-wrappers/$kotlinVersion/js"
            }
            from(wrapperProject.configurations['kotlinJVM']) {
                into "WEB-INF/kotlin-wrappers/$kotlinVersion/kotlin"
            }
        }
    }
}

project("kotlin.web.demo.executors") {
    dependencies {
        compile group: 'junit', 'name': 'junit', 'version': '4.12'
    }

    task copyToLib(type: Copy) {
        into "$buildDir/libs"
        from configurations.compile
    }
    jar.dependsOn(copyToLib)
}

project("kotlin.web.demo.server") {
    apply plugin: 'war'

    dependencies {
        compile group: 'org.scribe', name: 'scribe', version: '1.3.7'
        compile group: 'org.jetbrains', name: 'annotations', version: '13.0'
        compile 'org.twitter4j:twitter4j-core:4.0.4'
        compile 'com.nimbusds:nimbus-jose-jwt:3.9'
        compile 'org.pegdown:pegdown:1.5.0'
        runtime 'org.tuckey:urlrewritefilter:4.0.4'
        providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    }

    war {
        dependsOn ':kotlin.web.demo.frontend:build'
        archiveName "WebDemoWar.war"

        from('examples') {
            into 'examples'
        }
        from('help')
        from("${project(':kotlin.web.demo.frontend').projectDir}/static") {
            into 'static'
        }
        from("${project(':kotlin.web.demo.frontend').buildDir}") {
            into 'static'
            include 'kotlin.web.demo.frontend.js'
        }
        from("${project(':kotlin.web.demo.frontend').projectDir}/index.html")
        from("${rootProject.projectDir}/build.txt")
        for (kotlinVersion in kotlinVersions) {
            def wrapperProject = project(":kotlin.web.demo.backend:compilers:versions:$kotlinVersion")
            from(wrapperProject.configurations['kotlinJS'].collect { zipTree(it) }) {
                include "kotlin.js"
                into "static/kotlin/${kotlinVersion}"
            }
            from(project(":kotlin.web.demo.frontend").projectDir.absolutePath + File.separator + "static") {
                include "iframe.html"
                into "static/kotlin/${kotlinVersion}"
            }
        }
    }
}


project('kotlin.web.demo.test') {
    dependencies {
        testCompile project(':kotlin.web.demo.backend:compilers')
        testCompile project(':kotlin.web.demo.backend')
        testCompile project(':kotlin.web.demo.server')
        testCompile group: 'org.jetbrains.kotlin', 'name': 'kotlin-test', 'version': '1.1.51'
        testCompile group: 'junit', 'name': 'junit', 'version': '4.12'
    }
    for (kotlinVersion in kotlinVersions) {
        test.dependsOn(":kotlin.web.demo.backend:compilers:versions:$kotlinVersion:classes")
    }
    test.dependsOn(":kotlin.web.demo.executors:jar")
//    test.dependsOn(":copyKotlinLibs")
}

project(':kotlin.web.demo.backend:compilers:versions').jar { onlyIf { false } }